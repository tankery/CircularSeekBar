apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// This is the library version used when deploying the artifact
version = "1.3.0"
/**
 * v1.3.0:
 *  - API Change: Set glow effect disabled by default.
 *  - Upgrade gradle and sdk.
 *  - Fix drawing bug when glow effect enabled.
 * v1.2.0:
 *  - Upgrade gradle and sdk.
 *  - Add support to hide progress when empty
 *  - Fix some progress drawing bug
 * v1.1.7:
 *  - Upgrade gradle and sdk.
 * v1.1.6:
 *  - Add support for disabling progress glow.
 * v1.1.5:
 *  - Fix progress is hard to set to 0 or 100 when touch.
 * v1.1.4:
 *  - Upgrade gradle and sdk version
 *  - Fix Progress bar missing when progress is 100 on Android O.
 * v1.1.3:
 *  Remove unnecessary new instance of Path, and fix some bugs.
 * v1.1.2:
 *  Use dimension & color type for attrs instead of float & string...
 * v1.1.1:
 *  Fix resource name prefix error.
 * v1.1.0:
 *  add some Java object APIs.
 */

android {
    compileSdkVersion rootProject.compileSdkVersion
    resourcePrefix "cs_"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 29
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
}

def siteUrl = 'https://github.com/tankery/CircularSeekBar'
def gitUrl = 'https://github.com/tankery/CircularSeekBar.git'
group = "me.tankery.lib"
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Android CircularSeekBar view/widget'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'tankery'
                        name 'Tankery Chen'
                        email 'tankery.chen@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
if (rootProject.file('local.properties').exists()) {
    properties.load(rootProject.file('local.properties').newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "CircularSeekBar"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
