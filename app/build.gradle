apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// Define some common closures (methods) for code share
ext.getTagCount = {
    String cmd = "git tag -l \"v*\""
    String result = cmd.execute((String[])null, project.projectDir).text
    return result.trim().split("\n").length
}
ext.getLatestTaggedVersion = {
    String cmd = "git describe --abbrev=0 --tags"
    String result = cmd.execute((String[])null, project.projectDir).text
    return result.trim().replaceAll("v_?", "")
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "me.tankery.app.circularseekbar"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode getTagCount()
        versionName getLatestTaggedVersion()
    }
    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled false
            setProperty("archivesBaseName", "CircularSeekBar-demo-${defaultConfig.versionName}")
        }
    }
}

dependencies {
    implementation project(':circularSeekBar')
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
}
