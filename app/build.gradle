apply plugin: 'com.android.application'

// Define some common closures (methods) for code share
ext.getTagCount = {
    String cmd = "git tag -l \"v_*\""
    String result = cmd.execute((String[])null, project.projectDir).text
    return result.trim().split("\n").length
}
ext.getLatestTaggedVersion = {
    String cmd = "git describe --abbrev=0 --tags"
    String result = cmd.execute((String[])null, project.projectDir).text
    return result.trim().replace("v_", "")
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "me.tankery.app.circularseekbar"
        minSdkVersion 10
        targetSdkVersion 26
        versionCode getTagCount()
        versionName getLatestTaggedVersion()
    }
    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled false
            setProperty("archivesBaseName", "CircularSeekBar-demo-${defaultConfig.versionName}")
        }
    }
}

dependencies {
    compile project(':circularSeekBar')
    compile "com.android.support:support-annotations:${rootProject.supportLibVersion}"
}
